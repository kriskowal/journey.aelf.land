! t = 0
  x = 0
  y = 0
  from.t = 0
  from.x = 0
  from.y = 0

  river = 0
  forest = 1
  clearing = 2
  meadow = 3
  mountains = 4

  thirst = 0
  water = 0
  hunger = 0
  fish = 0
  deer = 0
  bottle = 20
  pack = 20

- @journey
  You begin your journey at ->time().
  {#(x#y)
  | A {#x1#y1|creek|stream|river} flows through here.
  | You find yourself among trunks of ->tree(x, y).
  | You stand in a clearing.
  | You stand in a beautiful meadow.
  | The view from the mountains takes your breath.
  }

  @setting
  + [ You c[C]ontinue east, ->travel(x, y, x+1, y) ]
    {+x} {+10t}
  + {(x > 0)}
    [ You r[R]eturn west, ->travel(x, y, x-1, y) ]
    {-x} {+10t}
  >
  ->timing()
  ->setting

- @travel(x1, y1, x2, y2)
  ! s1 = (#(x1#y1))%5
  ! s2 = (#(x2#y2))%5
  {(s1 == s2)?
    {(s2)
    | following the {#x1#y1|creek|stream|river}.
    | {~| continuing } through the forest.
    | {~| continuing } through the {#x1#y1|field|plain|clearing}.
    | {~| continuing } through the meadow.
    | {~| continuing } through the mountains.
    }
  | {(s1)
    | crossing the {#x1#y1|creek|stream|river}
    | emerging from the forest
    |
    | leaving the meadow
    | down
    }
    {(s2)
    | to the bank of a {~creek|stream|river}.
    | into a forest of ->tree(x2, y2).
    | to enter a {#x2#y2|field|plain|clearing}.
    | into a {~beautiful|lovely|} meadow.
    | up into the mountains.
    }
  }

- @timing()
  {(t/60<>from.t/60)?
    The hour rolls on.
    It is now ->time().
    ! from.t = t
  }

- @time()
  {@t/60|
  high don, the hour of the rowan|
  elm hour, the hour after high don|
  fir hour, the hour before high non|
  high non, with the sun directly overhead|
  cedar hour, the hour after high non|
  holly hour, the hour before high dusk|
  high dusk, the hour of the hawthorn|
  low dusk, the hour of the alder|
  ash hour, the hour after low dusk|
  oak hour, the hour before low non|
  low non, the hour of the elder and the middle of the night|
  birch hour, the hour after low non|
  yew hour, the hour before low don|
  low don, the hour of the willow}

- @tree(x, y)
  {#(x#y)|willow|rowan|elm|fir|cedar|holly|hawthorn|alder|ash|oak|elder|birch|yew}

